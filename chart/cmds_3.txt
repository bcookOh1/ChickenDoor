
set terminal pngcairo size 800,400
set output 'chart.png'
set timefmt '%Y-%m-%d %H:%M:%S'
set style line 1 lc 'dark-red' lw 2
set style line 2 lc 'blue' lw 2
set style line 3 lc 'yellow' lw 2
set title 'Chicken Coop Temperature, Humidity, and Light' font ',12'
set border 3 lw 2
set key autotitle columnhead
set key bottom right
set key font ',10'
set grid

set tics font ',10'
set tics nomirror

set xlabel 'hour:minute' font ',10'
set xlabel offset -1
set xdata time
set xtics rotate by 45 offset -2.3,-1.2
set format x '%H:%M'

set ylabel 'degF and %' font ',10'
set ytics 0,10
set ytics nomirror
set yrange [0:100]

set y2label 'light (lx)' font ',10'
set y2tics 0,100
set y2range [0:1000]

set datafile separator ','

plot 'readings.txt' using 2:3 w l ls 1 axis x1y1, 'readings.txt' using 2:4 w l ls 2 axis x1y1, 'readings.txt' using 2:5 w l ls 3 axis x1y2
